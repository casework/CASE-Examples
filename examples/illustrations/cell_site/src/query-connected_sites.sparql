SELECT DISTINCT ?lCellSiteIdentifier ?lCellSiteLocationAreaCode ?lConnectionStartTime ?lConnectionEndTime
WHERE
{
  ?nConnectedRelationship
    a uco-observable:ObservableRelationship ;
    uco-core:source kb:3fef85a7-3fb4-4170-ba4d-fc69e2de4789 ;
    uco-core:target ?nCellSite ;
    uco-core:kindOfRelationship "Connected_To"
    .
    OPTIONAL {
      ?nConnectedRelationship uco-observable:startTime ?lConnectionStartTime .
    }
    OPTIONAL {
      ?nConnectedRelationship uco-observable:endTime ?lConnectionEndTime .
    }

  ?nCellSite
    a uco-observable:CellSite ;
    uco-core:hasFacet ?nCellSiteFacet ;
    .

  ?nCellSiteFacet
    a uco-observable:CellSiteFacet ;
    .
    OPTIONAL {
      ?nCellSiteFacet uco-observable:cellSiteIdentifier ?lCellSiteIdentifier .
    }
    OPTIONAL {
      ?nCellSiteFacet uco-observable:cellSiteLocationAreaCode ?lCellSiteLocationAreaCode .
    }
}
