# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

SELECT DISTINCT ?lAddressValue ?lTransactionEndTime ?lNumericValue ?lUnitLabel
WHERE {
  ?nCryptoAddress
    a drafting:CryptoAddress ;
    ssn:hasProperty ?nBalanceProperty ;
    uco-core:hasFacet/drafting:addressValue ?lAddressValue ;
    .

  {
    ?nTransaction sosa:madeActuation ?nObservation .
    ?nObservation sosa:actsOnProperty ?nBalanceProperty .
  }
  UNION
  {
    ?nTransaction sosa:madeObservation ?nObservation .
    ?nObservation sosa:observedProperty ?nBalanceProperty .
  }

  ?nObservation
    sosa:hasResult ?nResult ;
    sosa:resultTime ?lTransactionEndTime ;
    .

  ?nResult
    a qudt:QuantityValue ;
    qudt:numericValue ?lNumericValue ;
    qudt:unit ?nUnit ;
    .

  ?nUnit
    a/rdfs:subClassOf* qudt:CurrencyUnit ;
    rdfs:label ?lUnitLabel ;
    .
}
ORDER BY ?lTransactionEndTime
